plugins {
    id 'java'
    id 'nu.studer.jooq' version '8.2.1'
}

group = 'xyz.inteligenciaativa'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    gradlePluginPortal()
    mavenCentral()
}

dependencies {
    // JOOQ e H2
    implementation 'org.jooq:jooq:3.18.6'
    jooqGenerator 'org.jooq:jooq:3.18.6'
    jooqGenerator 'org.jooq:jooq-meta:3.18.6'
    jooqGenerator 'org.jooq:jooq-codegen:3.18.6'
    jooqGenerator 'com.h2database:h2:2.2.224'

    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter:3.2.6'
    implementation 'org.springframework.boot:spring-boot-starter-web:3.2.6'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    // Testes
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.2.6'
}

jooq {
    version = '3.18.6'

    configurations {
        main {
            generateSchemaSourceOnCompilation = true
            generationTool {
                logging = 'WARN'

                jdbc {
                    driver = 'org.h2.Driver'
                    url = 'jdbc:h2:mem:testdb;DB_CLOSE_DELAY=-1'
                    user = 'sa'
                    password = ''
                }

                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'

                    strategy {
                        name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                    }

                    database {
                        name = 'org.jooq.meta.h2.H2Database'
                        inputSchema = 'PUBLIC' // H2 utiliza o schema PUBLIC por padr√£o
                    }

                    target {
                        packageName = 'xyz.inteligenciaativa.orderflow.repository.tables'
                        directory = 'build/generated-jooq'
                    }
                }
            }
        }
    }
}

tasks.named('build') {
    dependsOn 'generateJooq'
}